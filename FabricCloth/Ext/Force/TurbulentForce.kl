/// \version 1.0
/// \file TurbulentForce.kl
///
/// \brief Applies a noise force to the cloth simulation
///
/// \author Tom Minor, NCCA, Bournemouth University
/// \date 09/05/16
///
/// \todo Force stack currently isn't re-evaluated every frame, which means time based
///       turbulence is an issue.

require Geometry;

/// \dfgPresetFolder  Forces
object TurbulentForce : BaseForce
{
	Vec3 center;
	Float32 amplitude;
	Vec3 frequency;
  private Float32 time;
};

/// \dfgPresetFolder Forces
function TurbulentForce( Vec3 _center, Float32 _amplitude, Vec3 _frequency )
{
  // todo calculate center based on time
	this.center = _center;
	this.amplitude = _amplitude;
	this.frequency = _frequency;
  this.time = 0;
}

/// \dfgPresetFolder Forces
/// \internal
function TurbulentForce.initialise!()
{
	// Empty
}

function TurbulentForce.update!()
{
  this.time += 1;
}


/// \dfgPresetFolder Forces
/// \internal
function TurbulentForce.apply!(io Ref<GeometryAttributes> attrs)
{
  Ref<Vec3Attribute> positions = attrs.getPositions();
	Ref<Vec3Attribute> velocities = attrs.getOrCreateVec3Attribute('velocities');
  Ref<IntegerAttribute> movable = attrs.getOrCreateIntegerAttribute('movable');
  	
  // Update in parallel
  applyTurbulize<<<velocities.values.size()>>>(
                            positions.values, 
                            velocities.values, 
                            movable.values,
                            this.center + (Vec3(this.time) * this.frequency), 
                            this.amplitude,
                            this.frequency);
	
  velocities.incrementVersion();
}


operator applyTurbulize<<<index>>>(
          io Vec3 positions[],
				  io Vec3 velocities[],
  				Integer movable[],
  				Vec3 center, Float32 amplitude, Vec3 frequency)
{
  Float32 TIME_STEPSIZE2 = 0.5*0.5;

  // if ( !movable[index] ) {
    Vec3 q = center + frequency * positions[index];
    velocities[index] += Vec3(	amplitude * perlinNoise(q.x, q.z, q.y),
  				                      amplitude * perlinNoise(q.y, q.x, q.z),
      				                  amplitude * perlinNoise(q.z, q.y, q.x)
      		       	            );  
  // }
}